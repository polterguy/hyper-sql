/*
 * File responsible for executing specified __[sql]__ SQL.
 */





/*
 * Opening up a database connection.
 */
hyper-sql.database.get
p5.mysql.connect:[{0}]
  :x:/@hyper-sql.database.get?value

  /*
   * Executing SQL.
   */
  p5.mysql.select:x:/../*/sql?value
    limit:50

  /*
   * Making sure we notify any subscribers.
   */
  add:x:/+
    src:x:/@p5.mysql.select/*
  hyper-sql.sql.executed:x:/../*/sql?value

  /*
   * Storing SQL in session, such that we can populate the CodeMirror with
   * it, if user leaves page, and comes back later.
   */
  p5.web.session.set:hyper-sql.current-sql
    src:x:/../*/sql?value

  /*
   * Clearing old results, if existing.
   */
  clear-widget:hyper-sql-results-grid-wrapper

  /*
   * Verifying SQL actually yielded a result.
   */
  if:x:/@p5.mysql.select/*
    not
    micro.windows.info:SQL executed successfully but yielded no result
      class:micro-windows-info success
    return

  /*
   * Checking if it's possible to create charts out of the result.
   */
  if:x:/@p5.mysql.select/0/*?count
    =:int:2
    and:x:/@p5.mysql.select/*?count
      <:int:40
    and
      p5.types.can-convert:x:/@p5.mysql.select/0/1?value
        type:double

    /*
     * Graphs and charts can somehow be constructed from our above result.
     */
    add:x:/+/**/.data
      src:x:/@p5.mysql.select/*
    add:x:/../*/create-widgets
      src
        div
          class:right strip
          parent:hyper-sql-results-grid-wrapper
          widgets
            button
              innerValue:@"<span class=""icon-pie-chart""></span>"
              onclick

                /*
                 * Creating a pie chart in a modal widget, displaying result set.
                 */
                .data
                for-each:x:/@.data/*
                  set:x:/+2/*/*?name
                    src:x:/@_dp/#/0?value
                  eval-x:x:/+/*/*
                  add:x:/../*/create-widgets/*/*/*/micro.widgets.chart.pie/*/data
                    src
                      foo:x:/@_dp/#/1?value
                create-widgets
                  micro.widgets.modal
                    widgets
                      h3
                        innerValue:Pie chart of your data
                      micro.widgets.chart.pie
                        data

            button
              innerValue:@"<span class=""icon-stats-bars""></span>"
              onclick

                /*
                 * Creating a column chart in a modal widget, displaying result set.
                 */
                .data
                for-each:x:/@.data/*
                  set:x:/+2/*/*?name
                    src:x:/@_dp/#/0?value
                  eval-x:x:/+/*/*
                  add:x:/../*/create-widgets/*/*/*/micro.widgets.chart.column/*/data
                    src
                      foo:x:/@_dp/#/1?value
                create-widgets
                  micro.widgets.modal
                    widgets
                      h3
                        innerValue:Bar chart of your data
                      micro.widgets.chart.column
                        data

  /*
   * Creating column headers for our datagrid.
   */
  for-each:x:/@p5.mysql.connect/*/p5.mysql.select/0/*
    set:x:/+/*/*?name
      src:x:/@_dp/#?name
    add:x:/../*/create-widgets/*/micro.widgets.grid/*/columns
      src
        foo

  /*
   * Creating rows for our datagrid.
   */
  add:x:/+/*/*/items
    src:x:/..p5.mysql.connect/*/p5.mysql.select/[0,1]/*
  add:x:/../*/create-widgets/*/micro.widgets.grid/*/rows
    micro.evaluate.file:@HYPER-SQL/helpers/create-grid-rows.hl
      items

  /*
   * Checking if we had less than fifty items in our result,
   * at which point we delete the "feed grid with more items" button.
   */
  if:x:/@p5.mysql.connect/*/p5.mysql.select/*?count
    <:int:50
    set:x:/../*/create-widgets/*/div/=hyper-sql-feed-more-wrapper





/*
 * Creating a new datagrid, wrapping our result from the above
 * SQL execution.
 */
eval-x:x:/+/*/div/**(/.sql|/.database)
create-widgets
  micro.widgets.grid:hyper-sql-results-grid
    class:hyper-sql-datagrid striped hover
    parent:hyper-sql-results-grid-wrapper
    columns
    rows
  div:hyper-sql-feed-more-wrapper
    class:right
    parent:hyper-sql-results-grid-wrapper
    widgets
      button:hyper-sql-feed-more-btn
        innerValue:More ...
        title:Retrieves the next 50 records
        onclick:"return p5.hyper_sql_long_event ('hyper-sql-feed-more-btn', '.onclick')"
        .page:0
        .onclick

          /*
           * Forward evaluated above.
           */
          .database:x:/../*/hyper-sql.database.get?value
          .sql:x:/../*/sql?value

          /*
           * Retrieving current [.page], and incrementing it by 50.
           */
          get-widget-property:x:/../*/_event?value
            .page
          +:x:/@get-widget-property/*/*?value.int
            _:50
          set-widget-property:x:/../*/_event?value
            .page:x:/@+?value

          /*
           * Selecting items from database.
           */
          p5.mysql.connect:[{0}]
            :x:/@.database?value
            p5.mysql.select:x:/@.sql?value
              offset:x:/@+?value
              limit:50

          /*
           * Databinding grid.
           */
          add:x:/+/*/*/items
            src:x:/@p5.mysql.connect/*/p5.mysql.select/*
          add:x:/+
            micro.evaluate.file:@HYPER-SQL/helpers/create-grid-rows.hl
              items
          micro.widgets.grid.databind:hyper-sql-results-grid
            keep-items:true

          /*
           * Checking if this was our last page.
           */
          if:x:/@p5.mysql.connect/*/p5.mysql.select/*?count
            <:int:50
            delete-widget:hyper-sql-feed-more-wrapper
