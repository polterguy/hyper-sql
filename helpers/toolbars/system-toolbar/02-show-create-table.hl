
/*
 * Creates our "show create table" button.
 */
button
  innerValue:@"<span class=""icon-cog""></span>"
  title:Shows you the create table schema for your currently selected table
  disabled
  events


    /*
     * Invoked when for some reasons the active database is changed.
     */
    hyper-sql.database-explorer.selected-item-changed

      /*
       * Checking if selected object is a database, either directly or
       * indirectly - And if so, making sure we update the selected
       * value of our dropdown.
       */
      if:x:/../*/_arg?value
        =:table
        or:x:/../*/_arg?value
          =:column
        set-widget-property:x:/../*/_event?value
          .database:x:/../*/database?value
          .table:x:/../*/table?value
        delete-widget-property:x:/../*/_event?value
          disabled

      else

        /*
         * No database is currently selected, neither directly nor indirectly.
         */
        set-widget-property:x:/../*/_event?value
          disabled

  onclick

    /*
     * Retrieving active database and table, and figuring out how to reproduce
     * the table.
     */
    get-widget-property:x:/../*/_event?value
      .database
      .table
    p5.mysql.connect:[sys]
      p5.mysql.select:show create table `{0}`.`{1}`
        :x:/@get-widget-property/*/*/.database?value
        :x:/@get-widget-property/*/*/.table?value

      /*
       * Showing our "create table" SQL in a modal widget.
       */
      eval-x:x:/+/*/*/*/*/value
      create-widgets
        micro.widgets.modal:hyper-sql-show-create-table-modal
          widgets
            h3
              innerValue:Create table
            micro.widgets.codemirror
              mode:text/x-mysql
              value:x:/@p5.mysql.select/*/*/Create Table?value
            div
              class:right
              widgets
                button
                  title:Close modal window
                  class:large
                  innerValue:@"<span class=""icon-cross""></span>"
                  oninit

                    /*
                     * Setting initial focus to "close" button.
                     */
                    micro.page.set-focus:x:/../*/_event?value

                  onclick

                    /*
                     * Simply deleting modal widget.
                     */
                    delete-widget:hyper-sql-show-create-table-modal
