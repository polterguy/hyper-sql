
/*
 * Creates our "upload CSV file" button.
 */
micro.widgets.upload-button:hyper-sql-upload-csv-file
  disabled
  multiple
  events


    /*
     * Invoked when for some reasons the active database is changed.
     */
    hyper-sql.database-explorer.selected-item-changed

      /*
       * Checking if selected object is a table, either directly or
       * indirectly - And if so, making sure we enable the button,
       * and store the database/table.
       */
      if:x:/../*/_arg?value
        =:table
        or:x:/../*/_arg?value
          =:column
        set-widget-property:hyper-sql-upload-csv-file
          .database:x:/../*/database?value
          .table:x:/../*/table?value
        delete-widget-property:hyper-sql-upload-csv-file
          disabled

      else

        /*
         * No database is currently selected, neither directly nor indirectly.
         * Disabling the button.
         */
        set-widget-property:hyper-sql-upload-csv-file
          disabled

  .onupload

    /*
     * Retrieving active database and table, and importing uploaded CSV file into table.
     */
    get-widget-property:hyper-sql-upload-csv-file
      .database
      .table
    p5.mysql.connect:[{0}]
      :x:/@get-widget-property/*/*/.database?value

      /*
       * Making sure we wrap our insert in a transaction, to avoid importing
       * "half" a file due to exceptions occurring.
       */
      p5.mysql.transaction.begin

        /*
         * Iterating through each file uploaded.
         */
        .no:long:0
        for-each:x:/../*/files/*?value
          p5.mysql.load-from
            infile:x:/@_dp?value
            table:x:/@get-widget-property/*/*/.table?value
          set:x:/@.no?value
            +:x:/@.no?value
              _:x:/@p5.mysql.load-from?value

        /*
         * Committing transaction.
         */
        p5.mysql.transaction.commit

        /*
         * Providing feedback to user.
         */
        micro.windows.info:{0} records successfully imported
          :x:/@.no?value
          class:micro-windows-info success
