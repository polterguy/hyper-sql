
/*
 * Creates our "shortcut" button, that populates the CodeMirror editor
 * with "select * from database.table" and executes it.
 */
button
  innerValue:@"<span class=""icon-eye""></span>"
  disabled
  events


    /*
     * Invoked when for some reasons the active database is changed.
     */
    hyper-sql.object-explorer.selected-item-changed

      /*
       * Checking if selected object is a database, either directly or
       * indirectly - And if so, making sure we update the selected
       * value of our dropdown.
       */
      if:x:/../*/_arg?value
        =:table
        or:x:/../*/_arg?value
          =:column
        set-widget-property:x:/../*/_event?value
          .database:x:/../*/database?value
          .table:x:/../*/table?value
        delete-widget-property:x:/../*/_event?value
          disabled

      else

        /*
         * No database is currently selected, neither directly nor indirectly.
         */
        set-widget-property:x:/../*/_event?value
          disabled

  onclick

    /*
     * Retrieving active database and table, for then to populate CodeMirror
     * editor, and execute SQL.
     */
    get-widget-property:x:/../*/_event?value
      .database
      .table
    hyper-sql.codemirror.populate-with-sql:select * from `{0}`.`{1}`
      :x:/@get-widget-property/*/*/.database?value
      :x:/@get-widget-property/*/*/.table?value
    hyper-sql.execute-sql:select * from `{0}`.`{1}`
      :x:/@get-widget-property/*/*/.database?value
      :x:/@get-widget-property/*/*/.table?value
