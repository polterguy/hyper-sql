/*
 * File responsible for displaying user's stored snippets,
 * allowing him to load it into his CodeMirror instance.
 */
create-widgets
  micro.widgets.modal:hyper-sql-load-snippet
    widgets
      h3
        innerValue:Load SQL snippet

      /*
       * Grid containing available snippets.
       */
      micro.widgets.grid:hyper-sql-load-snippet-grid
        class:hover grid
        columns
          Name
          Delete
            style:"width:5px;"
        oninit

          /*
           * Invoking event responsible for databinding datagrid.
           */
          hyper-sql.snippets.databind

        events


          /*
           * Invoked when datagrid needs to be databound for some reasons.
           */
          hyper-sql.snippets.databind

            /*
             * Verifying that SQL snippets folder actually exists.
             */
            if
              fetch:x:/0/0?value
                folder-exists:~/documents/private/hyper-sql/

              /*
               * SQL snippets folder exists, listing all ".sql" files in it,
               * andpopulating our datagrid.
               */
              list-files:~/documents/private/hyper-sql/
                filter:.sql
              for-each:x:/@list-files/*?name
                split:x:/@_dp?value
                  =:/
                eval-x:x:/+/*/*/*/Name|/+/**/.name
                add:x:/..if/*/micro.widgets.grid.databind
                  src
                    item
                      .row
                        style:"cursor:pointer;"
                        onclick

                          /*
                           * Loading snippet, and closing modal widget.
                           */
                          .name:x:/@split/0/-?name
                          load-file:~/documents/private/hyper-sql/{0}
                            :x:/@.name?value
                          hyper-sql.sql-editor.set-sql:x:/@load-file/*?value
                          delete-widget:hyper-sql-load-snippet

                      Name:x:/@split/0/-?name
                      Delete
                        widgets
                          button
                            innerValue:@"<span class=""icon-bin""></span>"
                            onclick

                              /*
                               * Deleting file, and deleting row.
                               */
                              .name:x:/@split/0/-?name
                              delete-file:~/documents/private/hyper-sql/{0}
                                :x:/@.name?value
                              p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                element:tr
                              delete-widget:x:/-/*/*?value

              /*
               * Databinding our datagrid.
               */
              micro.widgets.grid.databind:x:/../*/_event?value
                items

      /*
       * Wrapper around Load/Close buttons.
       */
      div
        class:right
        widgets
          button:hyper-sql-close-load-snippet-button
            innerValue:@"<span class=""icon-cross""></span>"
            class:large
            onkeydown:@"if (event.keyCode == 27) {p5.$('hyper-sql-close-load-snippet-button').raise('onclick');return false;}"
            oninit

              /*
               * Setting initial focus to "close button".
               */
              micro.page.set-focus:x:/../*/_event?value

            onclick

              /*
               * Simply deletes modal widget.
               */
              delete-widget:hyper-sql-load-snippet
